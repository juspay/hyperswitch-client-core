// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 23
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "26.1.10909125"
        kotlinVersion = "1.9.24"
        kotlin_version = "1.9.24"
        androidXBrowser = "1.5.0"
        agp_version = '8.0.0'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:$agp_version")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
    gradle.startParameter.excludedTaskNames.addAll(
        gradle.startParameter.taskNames.findAll { it.contains("testClasses") }
    )
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url("$rootDir/maven/")
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://maven.hyperswitch.io/release/production/android/maven/1.3.9"
        }
        maven {
            url 'https://x.klarnacdn.net/mobile-sdk/'
        }
    }
}

apply plugin:'maven-publish'

def runtimeDeps = [
        'react-native-code-push',
        'react-native-gesture-handler',
        'react-native-inappbrowser-reborn',
        'react-native-pager-view',
        'react-native-safe-area-context',
        'react-native-screens',
        'react-native-svg',
        'sentry_react-native',
]

def runtimeDepsExcluded = []

def runtimeDepsOptional = [
        'react-native-reanimated',
        'react-native-hyperswitch-kount',
        'react-native-hyperswitch-paypal',
        'react-native-klarna-inapp-sdk',
        'react-native-hyperswitch-scancard',
        'react-native-hyperswitch-netcetera-3ds',
]

subprojects {
    if(tasks.findByPath(':react-native-klarna-inapp-sdk:compileDebugUnitTestJavaWithJavac')) {
        tasks.findByPath(':react-native-klarna-inapp-sdk:compileDebugUnitTestJavaWithJavac').enabled = false
    }
    if(tasks.findByPath(':react-native-klarna-inapp-sdk:compileReleaseUnitTestJavaWithJavac')) {
        tasks.findByPath(':react-native-klarna-inapp-sdk:compileReleaseUnitTestJavaWithJavac').enabled = false
    }
    // tasks.findByPath(':react-native-klarna-inapp-sdk:testReleaseUnitTest').enabled = false
    // tasks.findByPath(':react-native-hyperswitch-paypal:lintDebug').enabled = false

    publishing {
        publications {
            if (!runtimeDepsExcluded.contains(project.name)) {
                "$project.name"(MavenPublication) {
                    groupId project.group
                    artifactId(project.name == 'app' ? 'hyperswitch-sdk-android' : project.name)
                    version(project.name == 'app' ? project.version : '0.0.3')
                    artifact "$buildDir/outputs/aar/$project.name-release.aar"
                    pom.withXml {
                        def root = asNode()
                        root.appendNode('name', 'Hyperswitch SDK for Android')
                        root.appendNode('description', 'This is the Hyperswitch SDK for Android, providing seamless integration with the Hyperswitch platform.')
                        root.appendNode('url', 'http://www.hyperswitch.io')
                        def licensesNode = root.appendNode('licenses')
                        def licenseNode = licensesNode.appendNode('license')
                        licenseNode.appendNode('name', 'The Apache License, Version 2.0')
                        licenseNode.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                        licenseNode.appendNode('distribution', 'repo')
                        def scmNode = root.appendNode('scm')
                        scmNode.appendNode('url', 'https://github.com/hyperswitch/hyperswitch-sdk-android')
                        scmNode.appendNode('connection', 'scm:git:git://github.com/juspay/hyperswitch-sdk-android.git')
                        scmNode.appendNode('developerConnection', 'scm:git:ssh://git@github.com:juspay/hyperswitch-sdk-android.git')
                        scmNode.appendNode('tag', 'HEAD')
                        def developersNode = root.appendNode('developers')
                        def developerNode = developersNode.appendNode('developer')
                        developerNode.appendNode('id', 'sh-iv-am')
                        developerNode.appendNode('name', 'Shivam')
                        developerNode.appendNode('email', 'shivam.shashank@juspay.in')
                        def dependencies = root.appendNode('dependencies')
                        configurations.getByName('releaseCompileClasspath').getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                            if (!runtimeDepsOptional.contains(it.moduleName)) {
                                def dependency = dependencies.appendNode('dependency')
                                dependency.appendNode('groupId', it.moduleGroup)
                                dependency.appendNode('artifactId', it.moduleName)
                                dependency.appendNode('version', (runtimeDeps.contains(it.moduleName) ? '0.0.3' : it.moduleVersion))
                            }
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                url("${project.rootDir}/maven")
            }
        }
    }
}
